// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "34.0.0"
        minSdkVersion = 24
        compileSdkVersion = 34
        targetSdkVersion = 34
        androidXCore = "1.8.0"
        androidXAnnotation = "1.2.0"
        androidXBrowser = "1.3.0"
        ndkVersion = "26.1.10909125"
        kotlinVersion = "1.9.22"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:7.4.1")
        classpath("com.facebook.react:react-native-gradle-plugin")
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlinVersion"
        classpath 'com.google.gms:google-services:4.3.13'
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:deprecation"
            sourceCompatibility = JavaVersion.VERSION_17
            targetCompatibility = JavaVersion.VERSION_17
        }
    }
}
apply plugin: "com.facebook.react.rootproject"

allprojects {
    repositories {
        def REPOSITORY_URL = 'https://repo1.maven.org/maven2'
        all { ArtifactRepository repo ->
            if(repo instanceof MavenArtifactRepository){
                def url = repo.url.toString()
                if (url.startsWith('https://jcenter.bintray.com/')) {
                    project.logger.lifecycle "Repository ${repo.url} replaced by $REPOSITORY_URL."
                    remove repo
                }
            }
        }
        maven {
            url REPOSITORY_URL
        }
        mavenLocal()
        google()
        maven {
            url "$rootDir/../node_modules/detox/Detox-android"
        }
        maven {
            url "$rootDir/../node_modules/expo-camera/android/maven"
        }
    }
    configurations.all {
        resolutionStrategy {
            force 'androidx.core:core:1.7.0'

            dependencySubstitution {
                substitute module("com.redmadrobot:input-mask-android:6.0.0") using module('com.github.RedMadRobot:input-mask-android:6.0.0')
            }
            force "androidx.core:core:$rootProject.ext.androidXCore"
            force "androidx.core:core-ktx:$rootProject.ext.androidXCore"
        }
    }
}