// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "33.0.0"
        minSdkVersion = 24
        compileSdkVersion = 32
        targetSdkVersion = 32
        ndkVersion = "21.4.7075529"
        kotlinVersion = "1.6.20"
        androidXCore = "1.7.0"
        androidXAnnotation = "1.2.0"
        androidXBrowser = "1.3.0"
       // We use NDK 23 which has both M1 support and is the side-by-side NDK version from AGP.
        ndkVersion = "23.1.7779620"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:7.3.1")
        classpath("com.facebook.react:react-native-gradle-plugin")
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlinVersion"
        classpath 'com.google.gms:google-services:4.3.13'
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:deprecation"
        }
    }
}

allprojects {
    repositories {
        def REPOSITORY_URL = 'https://repo1.maven.org/maven2'
        all { ArtifactRepository repo ->
            if(repo instanceof MavenArtifactRepository){
                def url = repo.url.toString()
                if (url.startsWith('https://jcenter.bintray.com/')) {
                    project.logger.lifecycle "Repository ${repo.url} replaced by $REPOSITORY_URL."
                    remove repo
                }
            }
        }
        maven {
            url REPOSITORY_URL
        }
        mavenLocal()
        maven {
            url "$rootDir/../node_modules/detox/Detox-android"
        }
        maven {
            url "$rootDir/../node_modules/expo-camera/android/maven"
        }
        maven {
            url "https://appboy.github.io/appboy-android-sdk/sdk"
        }
    }
    configurations.all {
        resolutionStrategy {
            force 'androidx.core:core:1.7.0'

            // Needed for flipper https://github.com/facebook/flipper/issues/3397
            // In the case of a detox build
            force 'androidx.sqlite:sqlite-framework:2.1.0'
            dependencySubstitution {
                substitute module("com.redmadrobot:input-mask-android:6.0.0") using module('com.github.RedMadRobot:input-mask-android:6.0.0')
            }
            force "androidx.core:core:$rootProject.ext.androidXCore"
            force "androidx.core:core-ktx:$rootProject.ext.androidXCore"
        }
    }
}

subprojects {
    // Force all subprojects to use one and only one set of versions
    afterEvaluate {project ->
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion rootProject.ext.compileSdkVersion
                buildToolsVersion rootProject.ext.buildToolsVersion
                defaultConfig {
                    minSdkVersion rootProject.ext.minSdkVersion
                    targetSdkVersion rootProject.ext.targetSdkVersion
                }
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                    kotlinVersion = rootProject.ext.kotlinVersion
                }
            }
        }
    }
}
